plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

//如果当前的productFlavors不是app就引入firebase插件
//if (android.productFlavors.get('happyPuppy') != null) {
//    apply plugin: 'com.google.gms.google-services'
//}

android {
    namespace 'com.kola.webgame'
    compileSdk 33

    defaultConfig {
        applicationId "com.gamestudiox.main"
        minSdk 24
        targetSdk 33
        versionCode 101
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("..\\cashbreak.jks")
            storePassword "asdfghjkl123"
            keyAlias "cashbreak"
            keyPassword "asdfghjkl123"
        }
    }
    flavorDimensions "version"

    productFlavors {
        app {
            dimension "version"
        }

        popStone {
            dimension "version"
            applicationId "com.gamestudiox.popstone"
            versionCode 101
            versionName "1.0.1"
            buildConfigField "String", "H5NameSpace", "\"PopStone\""
            buildConfigField "String", "DefaultUrl", "\"https://cart.minigame.vip/game/popstone2/play\""
            buildConfigField "String", "DefaultOksUrl", "\"https://s.oksp.in/v1/spin/tml?pid=10772&appk=nZiTgPX3eXDXyIgBflNO49GO6gOTjxOF&did={did}\""
        }

        happyPuppy {
            dimension "version"
            applicationId "com.gamestudiox.happypuppycrush"
            versionCode 101
            versionName "1.0.1"
            buildConfigField "String", "H5NameSpace", "\"HappyPuppy\""
            buildConfigField "String", "DefaultUrl", "\"https://cart.minigame.vip/game/happypuppy-crush/play\""
            buildConfigField "String", "DefaultOksUrl", "\"https://s.oksp.in/v1/spin/tml?pid=10771&appk=SY0etlY44HvpWXP9yV6Q0nMOMBVtEw1N&did={did}\""

        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG", "true"
        }
        release {
            buildConfigField "boolean", "DEBUG", "false"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
    }

    sourceSets {
        popStone {
            java.srcDirs = ['src/popStone/java']
            res.srcDirs = ['src/popStone/res']
        }
        happyPuppy {
            java.srcDirs = ['src/happyPuppy/java']
            res.srcDirs = ['src/happyPuppy/res']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    //使用ViewBind
    buildFeatures {
        buildConfig true
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = applicationId + "_" + versionCode + "_release.apk"
        }
    }

}

dependencies {
    //包含libs下的.jar和.aar
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //autosize
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    //blankjUtils
    implementation 'com.blankj:utilcodex:1.31.1'
    //firebase
    implementation platform('com.google.firebase:firebase-bom:32.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    //gson
    implementation 'com.google.code.gson:gson:2.9.0'
    //hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"
    //coil
    implementation("io.coil-kt:coil:2.3.0")
    implementation("io.coil-kt:coil-gif:2.3.0")
    //lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
}

kapt {
    correctErrorTypes true
}